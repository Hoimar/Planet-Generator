[gd_resource type="ShaderMaterial" load_steps=3 format=2]

[ext_resource path="res://addons/hoimar.planetgen/resources/textures/water_normal_2.png" type="Texture" id=1]

[sub_resource type="Shader" id=1]
code = "shader_type spatial;

render_mode specular_schlick_ggx, cull_disabled;

uniform vec4 albedo : hint_color;
uniform float proximity_fade_distance;
uniform float planet_radius;
uniform sampler2D texture_refraction;
uniform float refraction : hint_range(-16,16);
uniform vec4 refraction_texture_channel;
uniform sampler2D texture_normal : hint_normal;

varying vec3 cam_position;

void vertex() {
	UV = UV * 500.0 + vec2(TIME * 0.15);
	cam_position = MODELVIEW_MATRIX[3].xyz;
}

void fragment() {
	vec2 base_uv = UV;
	ALBEDO = albedo.rgb;
	ROUGHNESS = 0.13;
	SPECULAR = 0.8;
	NORMALMAP = texture(texture_normal, base_uv).rgb;
	NORMALMAP_DEPTH = 0.03;
	RIM = 0.075;
	RIM_TINT = 0.75;
	// Refraction.
	vec3 ref_normal = normalize(mix(
			NORMAL,
			TANGENT * NORMALMAP.x + BINORMAL * NORMALMAP.y + NORMAL * NORMALMAP.z,
			NORMALMAP_DEPTH));
	vec2 ref_ofs = SCREEN_UV - ref_normal.xy * 
			dot(texture(texture_refraction, base_uv), vec4(1.0, 0.0, 0.0, 0.0)) * refraction;
	float ref_amount = 1.0 - albedo.a;
	EMISSION += textureLod(SCREEN_TEXTURE, ref_ofs, ROUGHNESS * 8.0).rgb * ref_amount;
	ALBEDO *= 1.0 - ref_amount;
	
	// Proximity fade.
	float depth_tex = textureLod(DEPTH_TEXTURE, SCREEN_UV, 0.0).r;
	vec4 world_pos = INV_PROJECTION_MATRIX * vec4(SCREEN_UV * 2.0 - 1.0, depth_tex * 2.0 - 1.0, 1.0);
	world_pos.xyz /= world_pos.w;
	ALPHA *= clamp(1.0 - smoothstep(world_pos.z + proximity_fade_distance, world_pos.z, VERTEX.z), 0.0, 1.0);
	
	// Fade out in the distance.
	ALPHA *= clamp(smoothstep(planet_radius*3.3, planet_radius*1.7, length(cam_position)), 0.0, 1.0);
}
"

[resource]
shader = SubResource( 1 )
shader_param/albedo = Color( 0.0431373, 0.258824, 0.219608, 0.839216 )
shader_param/proximity_fade_distance = 0.002
shader_param/planet_radius = null
shader_param/refraction = 0.06
shader_param/refraction_texture_channel = Plane( 1, 0, 0, 0 )
shader_param/texture_refraction = ExtResource( 1 )
shader_param/texture_normal = ExtResource( 1 )
